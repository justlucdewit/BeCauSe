// BCS E010
// Coded by luke_
// 31-10-2021

import "std/io"

// Macro's for constants
macro BOARD_SIZE  100 end
macro RULE_NUMBER 110 end
macro CHAR_SPACE  32 end
macro CHAR_ASTRIX 42 end
macro CHAR_ENTER  10 end

// Set the last cell to true
mem BOARD_SIZE 2 - + 1 .

// Loop from 0 to 29
0 while dup BOARD_SIZE 2 - < do

    // Loop over the memory chunk to print each cell
    0 while dup BOARD_SIZE 2 - < do
        dup mem + , if
            mem BOARD_SIZE + CHAR_ASTRIX .
        else
            mem BOARD_SIZE + CHAR_SPACE .
        end
        1 mem BOARD_SIZE + IO_WRITE

        1 +
    end
    drop
    
    // Print new line
    mem BOARD_SIZE + CHAR_ENTER .
    1 mem BOARD_SIZE + IO_WRITE

    // Compute next generation
    // (mem[0] << 1) | mem[1]
    mem , 1 <<
    mem 1 + , |

    1 while dup BOARD_SIZE 2 - < do
        swap 1 << 7 &
        over mem + 1 + , |
        2dup RULE_NUMBER swap >> 1 &
        swap mem + swap .
        swap

        1 +
    end
    drop drop

    1 +
end