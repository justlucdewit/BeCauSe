// BCS E010
// Coded by luke_
// 31-10-2021

// Macros to simplify IO
macro WRITE
    1 1 syscall3
end

// Macros to define constants
macro BOARD_SIZE    100 end
macro RULE          50 end
macro CHAR_NEWLINE  10 end
macro CHAR_SPACE    32 end
macro CHAR_ASTRIX   42 end

// Set the 28th cell to true
mem BOARD_SIZE 2 - + 1 .

// Loop from 0 to 29
0 while dup BOARD_SIZE < do

    // Loop over the memory chunk to print each cell
    0 while dup BOARD_SIZE < do
        dup mem + , if
            mem BOARD_SIZE + CHAR_ASTRIX .
        else
            mem BOARD_SIZE + CHAR_SPACE .
        end
        1 mem BOARD_SIZE + WRITE

        1 +
    end
    drop
    
    // Print new line
    mem BOARD_SIZE + CHAR_NEWLINE .
    1 mem BOARD_SIZE + WRITE

    // Compute next generation
    // (mem[0] << 1) | mem[1]
    mem , 1 <<
    mem 1 + , |

    1 while dup BOARD_SIZE 2 - < do
        swap 1 << 7 &
        over mem + 1 + , |
        2dup RULE swap >> 1 &
        swap mem + swap .
        swap

        1 +
    end
    drop drop

    1 +
end